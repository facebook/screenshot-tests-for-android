buildscript {
  apply from: rootProject.file('versions.gradle')
  repositories {
    jcenter()
    google()
  }

  dependencies {
    classpath plugs.agp
    classpath plugs.mavenGradle
  }
}

allprojects {
  version = property('VERSION_NAME')
  repositories {
    jcenter()
    google()
  }
}

def taskLogOutput = new StringBuilder()
gradle.addListener(new TaskExecutionListener() {
  def listener = new StandardOutputListener() {
    @Override
    void onOutput(CharSequence charSequence) {
      taskLogOutput.append(charSequence)
    }
  }

  @Override
  void beforeExecute(Task task) {
    task.logging.addStandardOutputListener(listener)
  }

  @Override
  void afterExecute(Task task, TaskState state) {
    task.logging.removeStandardOutputListener(listener)
  }
})

task cleanAll(dependsOn: [
    ':core:clean',
    ':plugin:clean',
    ':layout-hierarchy-common:clean',
    ':layout-hierarchy-litho:clean',
])

task installAll(dependsOn: [
    ':core:install',
    ':plugin:install',
    ':layout-hierarchy-common:install',
    ':layout-hierarchy-litho:install',
])

task uploadAllArchives(dependsOn: [
    ':plugin:uploadArchives',
    ':core:uploadArchives',
    ':layout-hierarchy-common:install',
    ':layout-hierarchy-litho:install',
])

task integrationTest(dependsOn: ':sample:runDebugAndroidTestScreenshotTest') {
  doLast {
    assert taskLogOutput.toString().contains('Found 11 screenshots')
  }
}
